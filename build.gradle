plugins {
    id 'java'
    id 'com.epam.drill.agent.runner.autotest' version '0.3.3'
}

group 'com.epam'
version '1.0-SNAPSHOT'

allprojects {

    repositories {
        mavenCentral()
    }

    dependencies {
        apply plugin: 'java'
        apply plugin: 'com.epam.drill.agent.runner.autotest'
        apply from: rootProject.projectDir.path + "/test2run.gradle"
        implementation 'io.rest-assured:rest-assured:4.3.3'
        implementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        runtimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        compile 'com.codeborne:selenide:5.19.0'
    }

    drill {
        version = "0.22.2"
        adminHost = "localhost"
        groupId = "realworld-app"
        adminPort = 8090
        additionalParams = [
            "devToolsProxyAddress": "localhost:8093",
            "withJsCoverage": true,
            "devtoolsAddressReplaceLocalhost": "host.docker.internal",
        ]
    }
}


// 1. create folders
tasks.register('prepareDirs') {
    mustRunAfter clean
    doFirst {
        mkdir "./chromium-binary/"
        mkdir "./chrome-driver/"
    }
}

// 2. download driver and binary
tasks.register('downloadChromeDriver', Exec) {
    dependsOn('prepareDirs')
    commandLine 'curl', '-L', '-o', './chrome-driver/chromedriver_win32.zip',
            project.findProperty('chromeDriverUrl')
}
tasks.register('downloadChromiumBinary', Exec) {
    dependsOn('prepareDirs')
    commandLine 'curl', '-L', '-o', './chromium-binary/chrome-win.zip',
            project.findProperty('chromeBinaryUrl')
}

// 3. unzip files
def registerUnzipTask(taskName, src, dest) {
    tasks.register(taskName, Copy) {
        dependsOn('downloadChromiumBinary', 'downloadChromeDriver')
        from zipTree(src)
        into dest
    }
}

registerUnzipTask('unzipChrome', './chromium-binary/chrome-win.zip', './chromium-binary/')
registerUnzipTask('unzipChromeDriver', './chrome-driver/chromedriver_win32.zip', './chrome-driver/')

// 4. delete zip files
tasks.register('cleanUp', Delete) {
    dependsOn('unzipChrome', 'unzipChromeDriver')
    delete './chrome-driver/chromedriver_win32.zip',
            './chromium-binary/chrome-win.zip'
}

// 5. register tasks
tasks.named('assemble') {
    dependsOn('prepareDirs', 'downloadChromeDriver', 'downloadChromiumBinary', 'unzipChrome', 'unzipChromeDriver', 'cleanUp')
}